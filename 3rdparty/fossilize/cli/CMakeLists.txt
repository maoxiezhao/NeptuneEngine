add_library(volk STATIC volk/volk.c volk/volk.h)
target_include_directories(volk PUBLIC ${CMAKE_CURRENT_SOURCE_DIR}/volk ${CMAKE_CURRENT_SOURCE_DIR}/../khronos)
target_compile_options(volk PRIVATE ${FOSSILIZE_CXX_FLAGS})
if (NOT WIN32)
	target_link_libraries(volk dl)
endif()

set(SPIRV-Headers_SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/SPIRV-Headers" CACHE STRING "SPIRV-Headers path")
set(SHADERC_SKIP_TESTS ON)
set(SPIRV_WERROR OFF CACHE BOOL "" FORCE)
add_subdirectory(SPIRV-Tools EXCLUDE_FROM_ALL)
add_subdirectory(SPIRV-Cross EXCLUDE_FROM_ALL)

add_library(cli-utils STATIC
		cli_parser.cpp cli_parser.hpp
		device.hpp device.cpp
		file.hpp file.cpp
		fossilize_feature_filter.hpp fossilize_feature_filter.cpp)
target_compile_options(cli-utils PRIVATE ${FOSSILIZE_CXX_FLAGS})
target_include_directories(cli-utils PUBLIC ${CMAKE_CURRENT_SOURCE_DIR})
target_include_directories(cli-utils PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/SPIRV-Headers/include/spirv/unified1)
target_link_libraries(cli-utils volk)
if (ANDROID)
	target_link_libraries(cli-utils log)
endif()

function(add_fossilize_cli NAME)
	add_executable(${NAME} ${ARGN})
	target_compile_options(${NAME} PRIVATE ${FOSSILIZE_CXX_FLAGS})
	target_link_libraries(${NAME} fossilize cli-utils)
	if (FOSSILIZE_SANITIZE_ADDRESS OR FOSSILIZE_SANITIZE_THREADS)
		set_target_properties(${NAME} PROPERTIES LINK_FLAGS "${FOSSILIZE_LINK_FLAGS}")
	endif()
	install(TARGETS ${NAME} DESTINATION ${CMAKE_INSTALL_BINDIR})
endfunction()

add_fossilize_cli(fossilize-replay fossilize_replay.cpp)
if (WIN32)
	target_sources(fossilize-replay PRIVATE fossilize_replay_windows.hpp)
else()
	target_sources(fossilize-replay PRIVATE fossilize_replay_linux.hpp)
endif()

if (APPLE OR ANDROID)
	target_compile_definitions(fossilize-replay PRIVATE NO_ROBUST_REPLAYER=1)
endif()

option(FOSSILIZE_REPLAYER_SPIRV_VAL "Enable support for SPIR-V validation in replayer." ON)
if (FOSSILIZE_REPLAYER_SPIRV_VAL)
	target_compile_definitions(fossilize-replay PRIVATE FOSSILIZE_REPLAYER_SPIRV_VAL)
	target_link_libraries(fossilize-replay SPIRV-Tools)
endif()

add_fossilize_cli(fossilize-bench fossilize_bench.cpp)
add_fossilize_cli(fossilize-convert-db fossilize_convert_db.cpp)
add_fossilize_cli(fossilize-merge-db fossilize_merge_db.cpp)
add_fossilize_cli(fossilize-disasm fossilize_disasm.cpp)
target_link_libraries(fossilize-disasm SPIRV-Tools spirv-cross-c)
add_fossilize_cli(fossilize-prune fossilize_prune.cpp)
add_fossilize_cli(fossilize-list fossilize_list.cpp)
add_fossilize_cli(fossilize-rehash fossilize_rehash.cpp)
add_fossilize_cli(fossilize-opt fossilize_opt.cpp)
target_link_libraries(fossilize-opt SPIRV-Tools-opt)
add_fossilize_cli(fossilize-synth fossilize_synth.cpp)
target_link_libraries(fossilize-synth spirv-cross-c)
